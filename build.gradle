buildscript {
    ext {
        junit_jupiter_version = '5.7.0'
    }
}

plugins {
    id "java"
    id 'org.springframework.boot' version '2.4.4' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'com.google.cloud.tools.jib' version '2.8.0' apply false
    id 'io.freefair.lombok' version '5.3.0' apply false
}

java {
    sourceCompatibility = org.gradle.api.JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
    }

    group = 'org.bdb.istio.starter'
    version = '0.0.1'

    apply plugin: 'java'
    apply plugin: 'io.freefair.lombok'

    test {
        useJUnitPlatform()
    }
}

project(':istio-starter-books') {
    dependencies {
        "implementation"(project(':istio-starter-domain'))
    }
}

project(':istio-starter-frontend') {
    dependencies {
        "implementation"(project(':istio-starter-domain'))
    }
}

['istio-starter-frontend', 'istio-starter-books'].each { name ->
    project(":$name") {
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
        apply plugin: 'com.google.cloud.tools.jib'


        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-web'

            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_jupiter_version}"
            testImplementation "org.junit.jupiter:junit-jupiter-engine:${junit_jupiter_version}"

            runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
        }
    }
}


